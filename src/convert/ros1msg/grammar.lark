// Grammar for parsing ROS1 message type definition file.
//
// The file contains the concatenated .msg definitions, divided by separators. The content should
// be the same as output of the `gendeps --cat` command.
//
// References:
// - https://wiki.ros.org/msg
// - https://wiki.ros.org/roslib/gentools
// - https://mcap.dev/spec/registry#ros1msg

start: line*

line: "MSG:" COMPLEX_TYPE COMMENT? NEWLINE              -> type
    | BUILTIN_TYPE ARRAY? NAME COMMENT? NEWLINE         -> builtin
    | COMPLEX_TYPE ARRAY? NAME COMMENT? NEWLINE         -> complex
    | BOOL_TYPE NAME "=" BOOL_VALUE COMMENT? NEWLINE    -> constant
    | UINT_TYPE NAME "=" INT COMMENT? NEWLINE           -> constant
    | INT_TYPE NAME "=" SIGNED_INT COMMENT? NEWLINE     -> constant
    | FLOAT_TYPE NAME "=" FLOAT_VALUE COMMENT? NEWLINE -> constant
    | CHAR_TYPE NAME "=" INT COMMENT? NEWLINE           -> constant
    | BYTE_TYPE NAME "=" SIGNED_INT COMMENT? NEWLINE    -> constant
    | STRING_TYPE NAME "=" STRING? NEWLINE              -> constant
    | COMMENT NEWLINE                                   -> comment
    | SEPARATOR NEWLINE                                 -> separator
    | NEWLINE                                           -> empty

// Types

COMPLEX_TYPE: MESSAGE_NAME | PACKAGE_NAME MESSAGE_NAME

BUILTIN_TYPE: BOOL_TYPE | UINT_TYPE | INT_TYPE | FLOAT_TYPE | CHAR_TYPE | BYTE_TYPE | STRING_TYPE | TIME_TYPE | DURATION_TYPE

BOOL_TYPE: /bool/

UINT_TYPE: /uint8|uint16|uint32|uint64/

INT_TYPE: /int8|int16|int32|int64/

FLOAT_TYPE: /float32|float64/

CHAR_TYPE: /char/

BYTE_TYPE: /byte/

STRING_TYPE: /string/

TIME_TYPE: /time/

DURATION_TYPE: /duration/

// Values

BOOL_VALUE: "true" | "false" | "True" | "False" | "1" | "0"

FLOAT_VALUE: INT | SIGNED_INT | FLOAT | SIGNED_FLOAT

STRING: /[^\n\r]+/

// Other IDL tokens

PACKAGE_NAME: /([a-z][a-z0-9_]*(?<!_)\/)+/

MESSAGE_NAME: /[A-Z][a-zA-Z0-9]*/

NAME: /[a-zA-Z]\w*/

ARRAY: "[" INT? "]"

SEPARATOR: /={2,}/

COMMENT: /#[^\n]*/

%import common.NEWLINE
%import common.WS_INLINE
%import common.INT
%import common.SIGNED_INT
%import common.FLOAT
%import common.SIGNED_FLOAT

%ignore WS_INLINE
